#Область ПроцедурыФункцииНаСервере

&НаСервере
Процедура ЗаполнитьНаСервере()

	ДомаТЗ = ркОтборОбъектовСервер.ПолучитьРезультат(КомпоновщикНастроек, УникальныйИдентификатор,,"рцОтборДомов");
	МассивВидовРасчета = Новый Массив;
	Для Каждого стрВидыРасчетов Из ВидыРасчетов Цикл
		Если стрВидыРасчетов.Пометка Тогда
			МассивВидовРасчета.Добавить(стрВидыРасчетов.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДомаТЗ",ДомаТЗ);
	Запрос.УстановитьПараметр("ВидыРасчетов",МассивВидовРасчета);
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("НачалоПериода",НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",КонецМесяца(Период));
	Запрос.УстановитьПараметр("СвойствоНаличиеМОП",СвойствоНаличиеМОП);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДомаТЗ.Дом КАК Дом
	|ПОМЕСТИТЬ ДомаТЗ
	|ИЗ
	|	&ДомаТЗ КАК ДомаТЗ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Дом
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДомаТЗ.Дом
	|ПОМЕСТИТЬ втДомаСМОП
	|ИЗ
	|	ДомаТЗ КАК ДомаТЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ркЗначенияПериодическихСвойствОбъектов.СрезПоследних(&НачалоПериода, Свойство = &СвойствоНаличиеМОП) КАК ркЗначенияПериодическихСвойствОбъектовСрезПоследних
	|		ПО ДомаТЗ.Дом = ркЗначенияПериодическихСвойствОбъектовСрезПоследних.Объект
	|ГДЕ
	|	(ркЗначенияПериодическихСвойствОбъектовСрезПоследних.Значение = ИСТИНА
	|			ИЛИ &СвойствоНаличиеМОП = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяССылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ркСчетчики.Владелец КАК Дом,
	|	ркСчетчикиВидыРасчетов.ВидРасчета КАК ВидРасчета
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	втДомаСМОП КАК втДомаСМОП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ркСчетчики КАК ркСчетчики
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ркСчетчики.ВидыРасчетов КАК ркСчетчикиВидыРасчетов
	|			ПО ркСчетчики.Ссылка = ркСчетчикиВидыРасчетов.Ссылка
	|		ПО втДомаСМОП.Дом = ркСчетчики.Владелец
	|ГДЕ
	|	ркСчетчикиВидыРасчетов.ВидРасчета В(&ВидыРасчетов)
	|	И ркСчетчикиВидыРасчетов.ВидПоказаний = ЗНАЧЕНИЕ(Справочник.ркВидыПоказанийСчетчиков.Обычные)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втДомаСМОП.Дом,
	|	ркДомаНомативныеОбщедомовыеНачисления.ВидРасчета
	|ИЗ
	|	втДомаСМОП КАК втДомаСМОП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ркДома.НомативныеОбщедомовыеНачисления КАК ркДомаНомативныеОбщедомовыеНачисления
	|		ПО втДомаСМОП.Дом = ркДомаНомативныеОбщедомовыеНачисления.Ссылка
	|ГДЕ
	|	ркДомаНомативныеОбщедомовыеНачисления.ВидРасчета В(&ВидыРасчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Дом КАК Дом,
	|	ВТ.ВидРасчета КАК ВидРасчета,
	|	МАКСИМУМ(ркНачисления.Ссылка) КАК Документ
	|ИЗ
	|	ВТ КАК ВТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ркНачисления КАК ркНачисления
	|		ПО ВТ.Дом = ркНачисления.ДомДляРасчета
	|			И ВТ.ВидРасчета = ркНачисления.ВидРасчета
	|			И (ркНачисления.НаОбщедомовыеНужды)
	|			И (ркНачисления.ДатаРасчетногоПериода = &НачалоПериода)
	|			И (ркНачисления.Организация = &Организация)
	|			И (ркНачисления.Дата МЕЖДУ &НачалоПериода И &КонецПериода)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ.Дом,
	|	ВТ.ВидРасчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дом,
	|	ВидРасчета";
	
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(),"ДокументыТЗ");
	
	
 КонецПроцедуры
 
&НаСервере
Процедура ОтменитьПроведениеДокументов();
	
	Для Каждого стрДокументыТЗ Из ДокументыТЗ Цикл
		Если ЗначениеЗаполнено(стрДокументыТЗ.Документ) Тогда
			стрДокументыТЗ.Документ.ПолучитьОбъект().Записать(РежимЗаписиДокумента.ОтменаПроведения);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура РасчитатьНачисление(Знач Дата,Знач Дом,Знач ВидРасчета,Знач Организация,ДокСсылка)
	
	Если ЗначениеЗаполнено(ДокСсылка) Тогда
		ДокОбъект = ДокСсылка.ПолучитьОбъект();
	Иначе	
		ДокОбъект = Документы.ркНачисления.СоздатьДокумент();
	КонецЕсли;
	
	ДокОбъект.Дата = Дата;
	ДокОбъект.Организация = Организация;
	ДокОбъект.ВидРасчета = ВидРасчета;
	ДокОбъект.ДатаРасчетногоПериода = НачалоМесяца(Дата);
	ДокОбъект.НаОбщедомовыеНужды = Истина;
	ДокОбъект.ДомДляРасчета = Дом;
	ДанныеПоЕжемесячнымНачислениям = Документы.ркНачисления.РаспределитьПоказанияОДПУ(ДокОбъект.ДатаРасчетногоПериода, ДокОбъект.ДомДляРасчета, ДокОбъект.ВидРасчета);
	ДокОбъект.СоставНачислений.Загрузить(ДанныеПоЕжемесячнымНачислениям.Начисления);
	ДокОбъект.Комментарий = ДанныеПоЕжемесячнымНачислениям.Комментарий;
	ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	Попытка
		ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Сообщить(ОписаниеОшибки(),СтатусСообщения.Важное);
	КонецПопытки;

КонецПроцедуры

#КонецОбласти
	

#Область ОбработчикиФормыНаСервере

 &НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	ркОтборОбъектовСервер.Инициализировать(КомпоновщикНастроек, УникальныйИдентификатор, "рцОтборДомов");
КонецПроцедуры
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПерезаписыватьСуществующие = Истина;
	
	Период = ПараметрыСеанса.ркДатаТекущегоРасчетногоПериода;
	Если КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.Элементы.Количество() = 0 Тогда
		ркОтборОбъектовСервер.Инициализировать(КомпоновщикНастроек, УникальныйИдентификатор, "рцОтборДомов");
	КонецЕсли;
	
	
	МассивВидовРасчета = Новый Массив;
	Для Каждого стрВидыРасчетов Из ВидыРасчетов Цикл
		Если стрВидыРасчетов.Пометка Тогда
			МассивВидовРасчета.Добавить(стрВидыРасчетов.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Пометка = ВидыРасчетов.Количество()=0;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивВидовРасчета",МассивВидовРасчета);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ркСчетчикиВидыРасчетов.ВидРасчета В (&МассивВидовРасчета)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка,
	|	ркСчетчикиВидыРасчетов.ВидРасчета КАК Значение
	|ИЗ
	|	Справочник.ркСчетчики.ВидыРасчетов КАК ркСчетчикиВидыРасчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ркСчетчики КАК ркСчетчики
	|		ПО ркСчетчикиВидыРасчетов.Ссылка = ркСчетчики.Ссылка
	|ГДЕ
	|	НЕ ркСчетчики.ПометкаУдаления
	|	И ркСчетчики.Владелец ССЫЛКА Справочник.ркДома
	|
	|УПОРЯДОЧИТЬ ПО
	|	ркСчетчикиВидыРасчетов.ВидРасчета";                                                           
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВидыРасчетов.Добавить(Выборка.Значение,,Пометка Или Выборка.Пометка);
	КонецЦикла;
	
	
КонецПроцедуры

 
#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ПровестиИзменения(Команда)
	
	ИндикаторПрогресса = 0;
	Элементы.ИндикаторПрогресса.Видимость = Истина;
    ОбновитьОтображениеДанных();
	Если ПерезаписыватьСуществующие Тогда
		ПоказатьОповещениеПользователя("Отмена проведения документов",,,БиблиотекаКартинок.ОтменаПроведения);
		ОтменитьПроведениеДокументов();
	КонецЕсли;
	
	Элементы.ИндикаторПрогресса.МаксимальноеЗначение = ДокументыТЗ.Количество();
	
	ДокДата = КонецМесяца(Период)-1;
	Для Каждого стрДокументыТЗ Из ДокументыТЗ Цикл

		Документ = ?(ПерезаписыватьСуществующие,стрДокументыТЗ.Документ,Неопределено);
		ПоказатьОповещениеПользователя(Документ,,""+стрДокументыТЗ.Дом+" "+стрДокументыТЗ.ВидРасчета);

		РасчитатьНачисление(ДокДата,стрДокументыТЗ.Дом,стрДокументыТЗ.ВидРасчета,Организация,Документ);

		стрДокументыТЗ.Документ = Документ;
		
		ИндикаторПрогресса = ИндикаторПрогресса + 1;
        ОбновитьОтображениеДанных();
        ОбработкаПрерыванияПользователя();
		
	КонецЦикла;

	Предупреждение("Обработано "+ИндикаторПрогресса+" из "+ДокументыТЗ.Количество());
	ИндикаторПрогресса = 0;
	Элементы.ИндикаторПрогресса.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

#КонецОбласти
	
